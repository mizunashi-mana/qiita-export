---
title: Trifecta で拡張可能な構文解析パーサを作る
tags: Haskell Parser
author: Mizunashi_Mana
slide: false
---
## はじめに

この記事では，パーサライブラリの1つである [Trifecta](https://hackage.haskell.org/package/trifecta) を使用する場合の，パーサの作成方法を紹介する．また， [前回の記事](https://qiita.com/Mizunashi_Mana/items/d73162057ff276a3238a) で紹介した recursion schemes for higher algebra を使用したAST表現を利用して，拡張可能な再帰的パーサの簡易的な作成手法を提案する．

なお，以下のものを想定している．

* GHCのバージョン: 8.6.3
* 前提とするGHC拡張 
  - `BlockArguments`
  - `ConstraintKinds`
  - `DataKinds`
  - `FlexibleInstances`
  - `FlexibleContexts`
  - `GeneralizedNewtypeDeriving`
  - `GADTs`
  - `LambdaCase`
  - `MultiParamTypeClasses`
  - `OverloadedLabels`
  - `PolyKinds`
  - `QuantifiedConstraints`
  - `RankNTypes`
  - `RecordWildCards`
  - `ScopedTypeVariables`
  - `StandaloneDeriving`
  - `TypeApplications`
  - `TypeFamilies`
  - `TypeOperators`
* 前提とするモジュールインポート

    ```haskell
    import           Control.Category
    import           Prelude              hiding (foldl, id, (.))
    import           Data.Function        hiding (id, (.))
    import           Data.Kind
    import           Data.Coerce
    import           GHC.OverloadedLabels (IsLabel (..))
    import           GHC.Records          (HasField (..))
    import           Data.Functor
    import           Control.Applicative
    import           Control.Monad
    ```

## Trifecta について

Trifecta の特徴としては，以下のものがある．

* パーサコンビネータライブラリである．
* パーサコンビネータの統一インターフェース [parsers](https://hackage.haskell.org/package/parsers) に，コンビネータを一任している．
* エラー情報に，ターミナル出力を支援する分かりやすいPretty出力情報が含まれている．

また，他のパーサライブラリと比べ，

* 公式には， `String` と `ByteString` に対するパースのみ対応している(後述)．
* [Parsec](https://hackage.haskell.org/package/parsec) より少し速いぐらいの性能で， [Megaparsec](https://hackage.haskell.org/package/megaparsec) より遅い．
* ドキュメントがほとんど存在しないが，パーサ自体は [parsers](https://hackage.haskell.org/package/parsers) のコンビネータを元に作るため，使い勝手は [Parsec](https://hackage.haskell.org/package/parsec) などとほぼ同じ．

のようになっている．手軽にパーサを作成したいなら Trifecta は1つの選択肢だが，後々の拡張性を考えるなら [Megaparsec](https://hackage.haskell.org/package/megaparsec) を検討するのがいいだろう．

Trifecta には，2つ重要な要素がある．それは，

* [`Rope`](https://hackage.haskell.org/package/trifecta-2/docs/Text-Trifecta-Rope.html#t:Rope) : 文字列を保持するデータ構造．細かい文字列を葉に持つ二分木によって1つの文字列を表し，インデックスアクセスの効率が良い．
* [`Delta`](https://hackage.haskell.org/package/trifecta-2/docs/Text-Trifecta-Delta.html#t:Delta) : 文字列の位置情報を表すデータ構造．

である．Trifecta では基本的に `Rope` に対してパース処理を行う．ただし，公式で `String` や `ByteString` から `Rope` への変換方法を提供しており，ユーザー視点では特に意識しなくて良い．もちろん，ユーザー定義で `Rope` に対しての変換方法を提供することも可能である．またパース時には `Delta` を保持しており，これは随時取得することもできるし，エラー情報としても使用できる．

以降では，具体的に使い方を見ていく．

## 基本的な使い方

基本的に必要となるモジュールは， [`Text.Trifecta` モジュール](https://hackage.haskell.org/package/trifecta-2/docs/Text-Trifecta.html) で再エクスポートされているため，このモジュールをインポートすれば良い．

パーサ本体は， [`Text.Trifecta.Parser` モジュール](https://hackage.haskell.org/package/trifecta-2/docs/Text-Trifecta-Parser.html) で提供されている．重要になる関数は，

* `stepParser :: Parser a -> Delta -> Step a` : パーサからパース処理をはじめる．
* `feed :: Reducer t Rope => t -> Step r -> Step r` : 文字列をパース処理の続きに渡す．
* `starve :: Step a -> Result a` : パース処理を完了させる．

である．なお，ここで `Reducer t Rope` が `t` を `Rope` に変換できることを表す制約と思えば良い[^about-reducer]．また `Step a` は `a` 型の値をパースする処理関数を表す． `Result a` は成功か失敗の2状態を持つ，パース結果を表すデータ型である．

[^about-reducer]: もう少しちゃんと言うと，このクラスは [reducer](https://hackage.haskell.org/package/reducers) パッケージで提供されているクラスで，異なるコンテナ間の `map` を提供するものだと思えば良い．このクラスのインスタンスを作成することで，他の文字列データ型にも対応することができる．

`stepParser` でパーサをパース処理に変換し，随時文字列を `feed` で渡し，最後に `starve` で完了させることでパース結果が得られる．なお，増分パース処理を行う必要がないとき用に，

```haskell
runParser :: Reducer t Rope => Parser a -> Delta -> t -> Result a
runParser p d s = starve $ feed s $ stepParser p d
```

という関数も提供されている． `Reducer ByteString Rope` や `Reducer String Rope` のインスタンスが提供されており， `runParser :: Parser a -> Delta -> ByteString -> Result a` 及び `runParser :: Parser a -> Delta -> String -> Result a` の型で使用できる．

`Delta` は， `Monoid` インスタンスが提供されており，初期 `Delta` として基本的に `mempty` を使っておけば問題ない．なお， `Delta` を作る方法としてはコンストラクタを自明に使えば良い．例えばファイルパースの際は， `Directed` を使って， `Directed filename 0 0 0 0` とすると良い．各コンストラクタの詳細については，[ドキュメント](https://hackage.haskell.org/package/trifecta-2/docs/Text-Trifecta-Delta.html#t:Delta)を参照してほしい．

Trifecta のパーサは，主に [parsers](https://hackage.haskell.org/package/parsers) によって作成する． [parsers](https://hackage.haskell.org/package/parsers) で提供されていないものは， [`Text.Trifecta.Combinators` モジュール](http://hackage.haskell.org/package/trifecta-2/docs/Text-Trifecta-Combinators.html)で提供されている． `parsers` や `Text.Trifecta.Combinators` は，コンビネータを型クラスによって抽象化して提供しており，それぞれの型クラスは，

* `Text.Parser.Combinators.Parsing` : 基本的なパーサコンビネータ．
* `Text.Parser.Char.CharParsing` : `Char` 文字列のパーサに対するコンビネータ．
* `Text.Parser.LookAhead.LookAheadParsing` : 先読み付きパーサ用のコンビネータ．
* `Text.Parser.Token.TokenParsing` : トークンパース用のコンビネータ．
* `Text.Trifecta.Combinators.DeltaParsing` : 現在の位置情報や消費された文字列を取得するコンビネータ．
* `Text.Trifecta.Combinators.MarkParsing` : 読み込み位置を変更するコンビネータ．

を提供する．通常は必要に応じて型制約を書くのが普通だが，それぞれ名前が長いので Trifecta を使う場合は必要に応じて次のエイリアスを作成して使うのが良い．

```haskell
import Text.Parser.Combinators
import Text.Parser.Char
import Text.Parser.Token
import Text.Trifecta.Combinators

type MonadicTokenParsing m =
  ( Parsing m
  , CharParsing m
  , TokenParsing m
  , DeltaParsing m
  , MonadPlus m
  )
```

例えば変数を含む，自然数のリストのパーサは次のように作成できる．

```haskell
import qualified Data.HashSet                as HashSet
import qualified Text.Parser.Token.Highlight as Highlight

identStyle :: MonadicTokenParsing m => IdentifierStyle m
identStyle = IdentifierStyle
  { _styleName = "natural list"
  , _styleStart = lower <|> oneOf "_"
  , _styleLetter = alphaNum <|> oneOf "_'"
  , _styleReserved = HashSet.fromList []
  , _styleHighlight = Highlight.Identifier
  , _styleReservedHighlight = Highlight.ReservedIdentifier
  }
  
identifier :: MonadicTokenParsing m => m String
identifier = ident identStyle
  
naturalList :: MonadicTokenParsing m => m [Either String Integer]
naturalList = symbol "[" *> (commaList1 <|> pure []) <* symbol "]"
  where
    commaList1 = do
      x <- item
      symbol "," $> (x:) <*> commaList1 <|> pure [x]
      
    item = Left <$> identifier <|> Right <$> natural
    
parseTokenString :: Parser a -> String -> Result a
parseTokenString p = runParser p' mempty
  where
    p' = whiteSpace *> p <* eof
```

`TokenParsing` のコンビネータは，余分な空白を消費しながらパース処理を行う．また，識別子処理用のコンビネータも用意されており， `Text.Parser.Token.IdentifierStyle` で識別子の情報を指定し，それを `Text.Parser.Token.ident` ， `Text.Parser.Token.reserve` に渡すことで識別子と予約語のパースを行う．実際に `naturalList` でパースを行ってみると次のようになる．

```haskell
>>> parseTokenString naturalList "[ 1,x,2 , y ]"
Success [Right 1,Left "x",Right 2,Left "y"]
>>> parseTokenString naturalList "[ 1,x,2 , y"
Failure (ErrInfo {_errDoc = (interactive):1:12: error: unexpected
    EOF, expected: ",", "]",
    letter or digit
[ 1,x,2 , y<EOF>
           ^     , _errDeltas = [Columns 11 11]})
```

なお，コメント処理を追加する場合， `Text.Parser.Token.Style.CommentStyle` と `Text.Parser.Token.Style.buildSomeSpaceParser` を使用できる．例えば Haskell のように， `{-` と `-}` での複数行コメントおよび `--` での行コメントを追加したい場合，次のように行う．

```haskell
import Text.Parser.Token.Style

commentStyle :: CommentStyle
commentStyle = CommentStyle
  { _commentStart   = "{-"
  , _commentEnd     = "-}"
  , _commentLine    = "--"
  , _commentNesting = True
  }
  
newtype ParsingWithComment m a = ParsingWithComment
  { runParsingWithComment :: m a
  }
  deriving
    ( Functor
    , Applicative
    , Monad
    , Alternative
    , MonadPlus
    , Parsing
    , CharParsing
    , DeltaParsing
    )
    
coerceParsingWithComment
  :: (m a -> m b) -> ParsingWithComment m a -> ParsingWithComment m b
coerceParsingWithComment = coerce

instance (CharParsing m, TokenParsing m) => TokenParsing (ParsingWithComment m) where
  someSpace = ParsingWithComment $ buildSomeSpaceParser someSpace commentStyle
  nesting = coerceParsingWithComment nesting
  semi = ParsingWithComment semi
  highlight = coerceParsingWithComment . highlight
  token p = coerceParsingWithComment token p <* whiteSpace

parseTokenStringWithComment :: ParsingWithComment Parser a -> String -> Result a
parseTokenStringWithComment = parseTokenString . runParsingWithComment
```

この場合，次のようにパースを行える．

```haskell
>>> parseTokenStringWithComment naturalList "[1, 2, {- comment -} x] -- line comment"
Success [Right 1,Right 2,Left "x"]
```

`DeltaParsing` を使うことで，パース結果に位置情報のアノテーション情報も付与できる．パース範囲を表すデータ型として `Span` が用意されている．これは単純に，開始位置の `Delta` と終了位置の `Delta` の組で表される[^more-detail-span]．この情報を例えば次のような関数でアノテーション情報として組みこめる．

[^more-detail-span]: 正確には，表示のために，パースした行の文字列も保持している．

```haskell
attachSpan :: DeltaParsing m => m (Span -> a) -> m a
attachSpan p = spanned p <&> \case
  f :~ x -> f x
```

以上のことを元に， Trifecta で拡張可能な相互再帰を含むパーサを作成する方法を見ていく．

## 拡張可能なパーサを作る

整数とブール値を交互に繰り返すリストを考える．これは次の文法で定義される．

```text
list ::= [ ilist ]
       | [ blist ]
       | [ ]

ilist ::= integer , blist
        | integer
        
blist ::= bool , ilist
        | bool

integer ::= <integer of trifecta>

bool ::= "true"
       | "false"
```

この文法に対するパーサを作成してみる．

```haskell
data IBList
  = ICons Int IBList
  | BCons Bool IBList
  | IBNil

list :: MonadicTokenParsing m => m IBList
list = symbol "[" *> (ilist <|> blist <|> pure IBNil) <* symbol "]"

ilist :: MonadicTokenParsing m => m IBList
ilist = do
  x <- integer
  ICons x <$> (blist <|> pure IBNil)
  
blist :: MonadicTokenParsing m => m IBList
blist = do
  x <- bool
  BCons x <$> (ilist <|> pure IBNil)

bool :: MonadicTokenParsing m => m Bool
bool
  =   symbol "True"  *> pure True
  <|> symbol "False" *> pure False
```

この文法に対して，整数要素には変数を許容するような言語拡張を提供することを考える．この場合，文法上は，

```text
ilist ::= ...
        | identifier blist
```

という変更を加えるだけで良い．しかし，既存のパーサを流用して拡張することは出来ない．これは，パーサが相互に再帰的に依存しあっているからである．このような拡張を容易に実現するのが，今回の目的である．この手法は，パーサを再帰的に定義するのでなく，open recursion をベースとした再帰的構造として定義する．具体的には，前回の記事で紹介した recursion schemes による表現を使用して，次のように書き換える．

```haskell
data IBListF r
  = IConsF Int r
  | BConsF Bool r
  | IBNil
  
data GrammarUnits m r = GrammarUnits
  { tokBool :: m Bool
  , tokInteger :: m Integer
  
  , list :: m r
  , ilist :: m r
  , blist :: m r
  }
  
grammarUnitsF :: MonadicTokenParsing m
  => (m (IBListF r) -> m r) -> GrammarUnits m r -> GrammarUnits m r
grammarUnitsF inj ~GrammarUnits{..} = GrammarUnits
  { tokBool
    =   symbol "True"  *> pure True
    <|> symbol "False" *> pure False
    
  , tokInteger = integer
  
  , list = symbol "[" *> (ilist <|> blist <|> pure IBNil) <* symbol "]"
  
  , ilist = do
    x <- tokInteger
    inj $ IConsF x <$> (blist <|> inj (pure IBNilF))
    
  , blist = do
    x <- tokBool
    inj $ BConsF x <$> (ilist <|> inj (pure IBNilF))
  }
  
grammarUnits :: MonadicTokenParsing m => m (Fix IBListF)
grammarUnits = fix $ grammarUnitsF (fmap In)
```

`grammarUnitsF` はパーサ本体でなく，パーサの再帰構造のみを定義し，それを `fix` で確定させることでパーサが得られる．イメージとしては，実際にパーサを使用するときにその依存関係から再帰が発生し，必要なパーサを順次取得しながらパーサが構築されていく．ただし注意点として，遅延パターンを指定しないと，再帰呼び出しにおいてまず `GrammarUnits` を紐解いてしまうため `fix` の際に無限ループが発生してしまう．遅延パターンを指定することで，依存解決の際に初めて `GrammarUnits` の分解が発生するようになる．この表現の利点は，先ほどの `identifier blist` を追加するようなことが，容易に実現できることである．

```haskell
data IBListExtF r
  = OriginalIBListF (IBListF r)
  | IdentConsF String r

data GrammarUnitsExt m r = GrammarUnitsExt
  { originalGU :: GrammarUnits m r
  , tokIdentifier :: m String
  }
  
grammarUnitsExtF :: MonadicTokenParsing m
  => (m (IBListExtF r) -> m r) -> GrammarUnitsExt m r -> GrammarUnitsExt m r
grammarUnitsExtF inj ~GrammarUnitsExt{..} = GrammarUnitsExt
  { originalGU = originalGU
    { ilist
      =   inj (IdentConsF <$> tokIdentifier <*> blist)
      <|> ilist originalGU
    }
    
  , tokIdentifier = identifier identStyle
  }
  
grammarUnitsExt :: MonadicTokenParsing m => m (Fix IBListExtF)
grammarUnitsExt = fix $ grammarUnitsExtF (fmap In)
```

これは，先ほどの文法の拡張とほぼ自然に対応できる．また，パーサの拡張は文法の拡張だけでなく， `inj` の部分を工夫することでアノテーション情報を付与しながら再帰させることもできる．この拡張には， cofree annotation を自然に適用することができる．

この手法を recursion schemes for higher algebra を元にして拡張することで，より柔軟な拡張が可能になる．例えば，以下の文法を考える．

```text
expr ::= \ var -> expr
       | let decls in expr
       | fexpr
       
fexpr ::= factor fexpr
        | factor
        
factor ::= var
         | lit
         | ( expr )

decls ::= { semidecls }

semidecls ::= decl ; semidecls
            | decl
            
decl ::= var = expr
            
var ::= identifier

lit ::= natural
```

これに対して，次のデータ型を用意する．

```haskell
type AstF = '[ExprF, DeclF, VarF, LitF]

data AstTag = ExprTag | DeclTag | VarTag | LitTag

data ExprF r i where
  LamAbsF :: r 'VarTag -> r 'ExprTag -> ExprF r 'ExprTag
  AppF :: r 'ExprTag -> [r 'ExprTag] -> ExprF r 'ExprTag
  LetF :: [r 'DeclTag] -> r 'ExprTag -> ExprF r 'ExprTag
  VarExprF :: r 'VarTag -> ExprF r 'ExprTag
  LitExprF :: r 'LitTag -> ExprF r 'ExprTag
  
data DeclF r i where
  DeclF :: r 'VarTag -> r 'ExprTag -> DeclF r 'DeclTag
  
data VarF r i where
  VarF :: String -> VarF r 'VarTag
  
data LitF r i where
  IntLitF :: Integer -> LitF r 'LitTag
```

このデータ型に対して，先ほどの `GrammarUnits` の表現を高階代数に対して定義することで，相互再帰を型安全により柔軟に扱える．

```haskell
data GrammarUnits m r = GrammarUnits
  { tokIdentifier :: m String
  , tokReserved :: String -> m ()
  , tokSymbol :: String -> m ()
  , tokNatural :: m Integer
  
  , expr :: m (r 'ExprTag)
  , fexpr :: m (r 'ExprTag)
  , factor :: m (r 'ExprTag)
  , decls :: m [r 'DeclTag]
  , decl :: m (r 'DeclTag)
  , var :: m (r 'VarTag)
  , lit :: m (r 'LitTag)
  }

appInj
  :: (Compose m (f r) :~> Compose m r)
  -> m (f r i) -> m (r i)
appInj (Nat inj) = getCompose . inj . Compose

grammarUnitsF :: MonadicTokenParsing m
  => (forall f. Member AstF f => Compose m (f r) :~> Compose m r)
  -> GrammarUnits m r -> GrammarUnits m r
grammarUnitsF inj ~GrammarUnits{..} = GrammarUnits
  { tokIdentifier = ident identStyle
  , tokReserved = reserve identStyle
  , tokSymbol = fmap (const ()) . symbol
  , tokNatural = natural
  
  
  , expr = appInj inj
    (   tokSymbol "\\" $> LamAbsF <*> var <* tokSymbol "->" <*> expr
    <|> tokReserved "let" $> LetF <*> decls <* tokReserved "in" <*> expr
    )
    <|> fexpr
    
  , fexpr = appInj inj
    $ AppF <$> factor <*> many factor
  
  , factor = appInj inj
    (   VarExprF <$> var
    <|> LitExprF <$> lit
    )
    <|> tokSymbol "(" *> expr <* tokSymbol ")"
    
  , decls = tokSymbol "{" *> semidecls <* tokSymbol "}"
  
  , decl = appInj inj
    (   DeclF <$> var <* tokSymbol "=" <*> expr
    )
  
  , var = appInj inj
    (   VarF <$> tokIdentifier
    )
  
  , lit = appInj inj
    (   IntLitF <$> tokNatural
    )
  }
  where
    semidecls = (:) <$> decl <*> many (tokSymbol ";" *> decl)
```

実際にこの再帰構造を再帰させ，アノテーション情報を付与する場合，次のような関数を用意する．

```haskell
type ParsedAstF = HCofreeF (HUnion AstF) (Const Span)
type ParsedAst = HFix ParsedAstF

injParsedAnn :: (MonadicTokenParsing m, Member AstF f)
  => Compose m (f ParsedAst) :~> Compose m ParsedAst
injParsedAnn = attachCofreeAnn . hfmap injectU
  where
    attachCofreeAnn = Nat \case
      Compose m -> Compose . fmap HIn . attachSpan $ toHCofree <$> m

    toHCofree x d = HCofreeF (Const d) x
    
grammarUnits :: MonadicTokenParsing m => GrammarUnits m ParsedAst
grammarUnits = fix $ grammarUnitsF injParsedAnn
```

`injParsedAnn` は `Span` をアノテーション情報として埋め込みながら再帰を行い，再帰構造から実際の再帰的なデータ型へと接着をさせる．この `injParsedAnn` を使って `GrammarUnits` では `ExprF` や `VarF` などの再帰構造を捉えた `HFunctor` を再帰構造に持ち上げている．後は， `injParsedAnn` を渡して `fix` で実際に再帰構造に接着させていくことで，パーサの集まりである `grammarUnits` が得られる．実際次のようにパースが行えていることが分かる．

```haskell
>>> parseTokenString (expr grammarUnits) "let { f = \\x -> x; x = 0 } in f x" *> pure ()
Success ()
>>> parseTokenString (expr grammarUnits) "let { f = \\x -> x; x = 0 } f x" *> pure ()
Failure (ErrInfo {_errDoc = (interactive):1:28: error: expected: "in"
let { f = \x -> x; x = 0 } f x<EOF>
                           ^        , _errDeltas = [Columns 27 27]})
```

では，実際にインデントルールを取り入れる拡張の例を見てみる．

## インデントルールに対応する

今回， `let-in` 構文を提供した． `let-in` 構文ではローカル定義を複数 `;` で区切って書ける．ただし，通常見やすいようこれらの定義はインデントを使って整形する場合が多い．その場合，インデントによって区切りが明確になるため， `;` を明示的に書くのは冗長な場合がある．その場合に，インデントによって区切りを分けるパースルールが幾つかの言語では設けられている．これらのルールは，インデントルール，レイアウトルール，オフサイドルールなどと呼ばれ，例えば Haskell や Python などで採用されている．

ただし，このパースルールはレクサとパーサの連携が必要で，通常書くのが難しい． Trifecta には簡単なインデントルールをパースするために， [`indentation-trifecta` パッケージ](https://hackage.haskell.org/package/indentation-trifecta) が提供されている．このパッケージを使って， `let-in` 構文にインデントルールを追加してみる．

`indentation-trifecta` には，インデントルールに対応するためのコンビネータを提供する `IndentationParsing` クラスと，標準実装を提供するパーサトランスフォーマの `IndentationParserT` が含まれている． `IndentationParserT` は `evalIndentationParserT` に初期のインデント状態と共に与えることで，インデントルールを加味したパーサを出力できる．ただし，インデント処理のためトークンの位置情報が必要になるため，元となるパーサは `TokenParsing` と `DeltaParsing` の実装を必要とする．インデントルールに対応するパーサは，以下のように提供できる．

```haskell
import           Text.Trifecta.Indentation hiding (IndentationRel (..), Token)
import qualified Text.Trifecta.Indentation as Indent

type MonadicIndentTokenParsing m =
  ( MonadicTokenParsing m
  , IndentationParsing m
  )

newtype IndentParser a = IndentParser
  (IndentationParserT Indent.Token Parser a)
  deriving
    ( Functor
    , Applicative
    , Monad
    , Alternative
    , MonadPlus
    , Parsing
    , CharParsing
    , DeltaParsing
    , IndentationParsing
    , TokenParsing
    )
    
initialIndentationState :: IndentationState
initialIndentationState = mkIndentationState 0 infIndentation True Indent.Ge

parseIndentTokenString :: IndentParser a -> String -> Result a
parseIndentTokenString (IndentParser p) = parseTokenString
  (evalIndentationParserT p initialIndentationState)
```

実際にインデントルールは， `IndentationParsing` のコンビネータで指定する．主に，

* `localIndentation` : インデントコンテキストを変更したパーサを作成する．
* `absoluteIndentation` : インデントの調整箇所を指定したパーサを作成する．

の2つを使うことでパーサが作成できる． `localIndentation` に指定するインデントコンテキストは，主に `Eq` / `Gt` / `Ge` があり，例えば `Gt` を指定すると，次のトークンの位置を見てその位置が現在のインデント以下の場合失敗し，それより上の場合パーサを実行する．

実際に `let-in` 構文への適用は，以下のようにできる．

```haskell
grammarUnitsWithIndent :: MonadicIndentTokenParsing m
  => GrammarUnits m (HFix ParsedAstF)
grammarUnitsWithIndent = fix $ grammarUnitsWithIndentF injParsedAnn

localIndentationGt :: IndentationParsing m => m a -> m a
localIndentationGt = localIndentation Indent.Gt

grammarUnitsWithIndentF :: MonadicIndentTokenParsing m
  => (forall f. Member AstF f => Compose m (f r) :~> Compose m r)
  -> GrammarUnits m r -> GrammarUnits m r
grammarUnitsWithIndentF inj ~us@GrammarUnits{..} =
  let us' = grammarUnitsF inj us
  in us'
    { decls
      =   localIndentationGt (many $ absoluteIndentation decl)
      <|> getField @"decls" us'
      
    , decl = appInj inj
      (   DeclF <$> var <* tokSymbol "=" <*> localIndentationGt expr
      )
    }
```

なお，今回 `decls` フィールドへのアクセスは， `RecordWildCards` 拡張による展開された変数と被るため使えない．そのため，GHC 8.6 で新たに登場した `GHC.Records.getField` を使用している．`GrammarUnits` による表現は，実際にこのようにパーサを容易に拡張することができる．実際にパースが成功することは，次のように確認できる．

```haskell
>>> parseIndentTokenString (expr grammarUnitsWithIndent) "let\n  f = \\x -> x\n  x = 0\n in f x" *> pure ()
Success ()
>>> parseIndentTokenString (expr grammarUnitsWithIndent) "let\n  f = \\x -> x\n x = 0\n in f x" *> pure ()
Failure (ErrInfo {_errDoc = (interactive):3:2: error: expected: "in"
 x = 0
 ^     , _errDeltas = [Lines 2 1 19 1]})
```

## まとめ

Trifecta によるパーサの作成方法について紹介した． Trifecta は読みやすいエラーメッセージをデフォルトで提供してくれるため，手軽に使う分には扱いやすい．

また，前回の記事で紹介した recursion schemes for higher algebra による手法と組み合わせることで，拡張可能なパーサを作る手法も紹介した．この手法により，文法に合わせた形での相互再帰パーサの拡張を行うことができる．

最後に， Trifecta でのインデントルールのパース方法の紹介と，実際にパーサを拡張する例を見せた．

次回は，静的スコープを持つ言語のインタプリタの実装方法について紹介する．

