---
title: ASTのための高階代数を元にした recursion schemes
tags: Haskell
author: Mizunashi_Mana
slide: false
---
## はじめに

Recursion schemes(再帰スキーム)とは，再帰的なデータ構造に対する汎用コンビネータの総称のことである．ところで， recursion schemes から導出される再帰構造を捉えた `Functor` (代数) による再帰データの表現は，コンパイラやインタプリタでASTを扱う際便利な場合が多い．しかし，この表現は相互再帰に弱いという欠点がある．ここでは，高階代数で同様のことを考えることにより，この欠点を克服する手法を紹介する．

なお，以下のものを想定している．

* GHCのバージョン: 8.6.3
* 前提とするGHC拡張
  - `BlockArguments`
  - `DataKinds`
  - `DeriveFunctor`
  - `FlexibleInstances`
  - `FlexibleContexts`
  - `FunctionalDependencies`
  - `GADTs`
  - `LambdaCase`
  - `MultiParamTypeClasses`
  - `OverloadedLabels`
  - `PolyKinds`
  - `QuantifiedConstraints`
  - `RankNTypes`
  - `ScopedTypeVariables`
  - `StandaloneDeriving`
  - `TypeFamilies`
  - `TypeOperators`
* 前提とするモジュールインポート

    ```haskell
    import           Prelude hiding ((.), id)
    import qualified Prelude
    import           Control.Category
    import           Data.Kind
    ```

## Recursion SchemesとAST

例えば，以下の再帰データを考えてみよう．

```haskell
data List a
  = Nil
  | Cons a (List a)
```

このデータは，よくある単連結リストを表現するもので，`Cons`コンストラクタで再帰的に自身の型を持つ要素を受け取る．これに対して以下のようなオペレータが考えられる．

```haskell
data ListF a r
  = NilF
  | ConsF a r

cataList :: (ListF a b -> b) -> (List a -> b)
cataList f = go
  where
    go Nil         = f NilF
    go (Cons x xs) = f (ConsF x (go xs))
```

`cataList` は Haskell では `foldr :: (a -> b -> b) -> b -> [a] -> b` という名前で知られている[^iso-foldr-and-cata]．ところで， `foldr` がそうであるように，このオペレータはリスト専用のものではなく，再帰構造から抽出した再帰の元となる構造をある種の `Functor` [^what-property-for-recursion-base-functor]と見做すことで，以下のように一般化できる．

[^iso-foldr-and-cata]: `ListF a b -> b` $\simeq$ `(() -> b, (a, b) -> b)` (直積と直和の汎用的表現への変形) $\simeq$ `(b, a -> b -> b)` (無駄な引数の除去及びカリー化) $\simeq$ `(a -> b -> b, b)` (要素の入れ替え)

[^what-property-for-recursion-base-functor]: ある種の `Functor` とは，(最小かつ最大)不動点を持つもの，主に多項式自己関手(polynomial endo-functor)と呼ばれるもののことを指す． `DeriveFunctor` 拡張を有効にして， `Functor` を `deriving` した時に作られるインスタンスのことと思ってもらって，問題ない．

```haskell
deriving instance Functor (ListF a)

newtype Fix f = In
  { out :: f (Fix f)
  }

cata :: Functor f => (f b -> b) -> (Fix f -> b)
cata f = go
  where
    go = f . fmap go . out

-- FB stands by "Functor Base"
type FBList a = Fix (ListF a)

cataFBList :: (ListF a b -> b) -> (FBList a -> b)
cataFBList = cata
```

ここで新しく， `Fix` というデータ型を導入した． `Fix (ListF a)` $\simeq$ `ListF a (Fix (ListF a))` $\simeq$ `ListF a (... (ListF a (...)) ...)` $\simeq$ `List a` となることに注目して欲しい．元々の再帰データは，そこから抽出した再帰構造を表す `Functor f` を使用して， `Fix f` という形で表すことができる(この操作を `f` から再帰データを固定すると言うことにする)．これは， `Data.Function.fix :: (a -> a) -> a` を型レベルにしたものと見ることができる[^notice-typelevel-fix]．このデータ型によって，何らかの `Functor` の実装(主に再帰構造を表す `Functor` )を提供すれば，オペレータ `cata` が定まるようになる．

[^notice-typelevel-fix]: 厳密には， `newtype` を使用しているため，異なる．もちろん，型シノニムの場合型システムが決定不能になるため， `type Fix f = f (Fix f)` という宣言は書けないようになっている．

このオペレータを元にした有用なオペレータはいくつか知られており(例えば，再帰中に全ての部分構造にアクセスできるようにした [para](https://hackage.haskell.org/package/recursion-schemes-5.0.3/docs/Data-Functor-Foldable.html#v:para) など)，これらを総称してrecursion schemesと呼ぶ[^notice-corecursion-schemes]．他のオペレータについては， [recursion-schemes](https://hackage.haskell.org/package/recursion-schemes) というパッケージにまとまっているので，参照して欲しい．

[^notice-corecursion-schemes]: なお，それぞれの双対版(unfold相当)を考えたものもあり， `cata` の双対 `ana :: Functor f => (b -> f b) -> (b -> Fix f)` を元に作る．

再帰データを再帰構造を表す `Functor` から `Fix` で固定した形式で表すことには，幾つか利点がある． `cata` のような有用なオペレータが導入できるというのも大きな利点だが，副次的な利点として再帰のオープン化がある．例えば

```haskell
type VarName = String

data Expr
  = LamAbs VarName Expr
  | App Expr Expr
  | Var VarName
  | NumLit Int
```

のようなデータ構造が外部ライブラリで公開されていた場合に，これに `BoolLit Bool` というコンストラクタを付け足したいということがよくある．しかし単純に `data ExprExt = Orig Expr | BoolLit Bool` とすれば良いわけではない．なぜなら，再帰的に `Expr` が指定されてる部分で， `BoolLit` が使えるようになる訳ではないからだ．これが， `Fix` で固定する表現では，次のように解決できる．

```haskell
type VarName = String

data ExprF r
  = LamAbsF VarName r
  | AppF r r
  | VarF VarName
  | NumLitF Int
  deriving Functor
  
type Expr = Fix ExprF


data (f :+: g) a = InL (f a) | InR (g a)
  deriving Functor

data BoolF r
  = BoolLitF Bool
  deriving Functor
  
type ExprExtF = Fix (ExprF :+: BoolF)
```

もちろん通常は，単にコンストラクタを追加するだけでなく周辺ユーティリティも流用したいことが多く，その辺も工夫が必要で，そのためのフレームワークとして [Data types a la carte](http://www.cs.ru.nl/~W.Swierstra/Publications/DataTypesALaCarte.pdf) という手法が知られている．

オープン化による恩恵はコンストラクタを付け足すことだけでなく，再帰構造に一定の付加情報を加える場合も役に立つ．再帰データ型に，再帰ごとにアノテーション情報を埋め込みたいことがよくある．これは， `Fix` を元にした表現では簡単に実現できる．

```haskell
data CofreeF f a r = CofreeF a (f r)
  deriving Functor

type ExprWithAnn a = Fix (CofreeF Expr a)
```

この手法は， `Fix (CofreeF f a)` $\simeq$ `Cofree f a` というデータ型にちなんで，cofree annotationと呼ばれる．

元の再帰構造とユーティリティを流用しつつ拡張できることは，言語拡張を扱う上で重要で，アノテーション情報を容易に埋め込める点から， `Fix` で固定する形式の表現方法は，ASTを扱う上で便利な場合が多い．

しかしながらこの手法は相互再帰に弱いという欠点がある．次のようなデータ型を， `Fix` を使って表すことを考える．

```haskell
type VarName = String

data Expr
  = LamAbs VarName Expr
  | App Expr Expr
  | Let [Decl] Expr
  | Var VarName
  | NumLit Int
  
data Decl
  = Decl VarName Expr
  

data ExprF r
  = LamAbsF VarName r
  | AppF r r
  | LetF [r] r
  | VarF VarName
  | NumLitF Int
  deriving Functor

data DeclF r
  = DeclF VarName r
  deriving Functor

cataExpr :: (ExprF b -> b) -> (DeclF b -> b) -> (Expr -> b)
cataExpr f g = goExpr
  where
    goExpr (LamAbs x e) = f (LamAbsF x (goExpr e))
    ...
    goExpr (Let ds e)   = f (LetF [ goDecl d | d <- ds ] (goExpr e))
    ...
    
    goDecl (Decl x e) = g (DeclF x (goExpr e))
```

`Expr` と `Decl` で相互再帰となっていることが分かる．このようなデータ型は単純には， `Fix` で表すことはできない[^why-choose-expr-to-main-data]．ただ，型安全性が失われることに目を瞑るなら，次のように表すことはできる．

[^why-choose-expr-to-main-data]: 例えば， `data ExprF r = ... | LetF [Decl r] r | ...` ， `data DeclF r = DeclF VarName r` といった形で `Expr` を基軸に `Functor` を定義できるのではないかと思う人はいるかもしれない．しかし，例えばソースの位置情報をcofree annotationで付加することを考える時，この方法だと `Decl` にはアノテーションが付けられない．

```haskell
type FBExpr = Fix (ExprF :+: DeclF)

cataFBExpr :: ((ExprF :+: DeclF) a -> a) -> (FBExpr -> a)
cataFBExpr f = fst . cata alg
  where
    alg x = let nx = f (fmap fst x) in case x of
      InL (LamAbsF _ e) -> checkL e nx
      InL (AppF e1 e2)  -> checkL e1 (checkL e2 nx)
      InL (LetF ds e)   -> foldr checkR (checkL e nx) ds
      InR (DeclF _ e)   -> checkL e nx
      InL _             -> (nx, Left ())
      
    checkL (_, Left ()) x = x
    checkL _            _ = error "expect ExprF"
    
    checkR (_, Right ()) x = x
    checkR _             _ = error "expect DeclF"
```

この方法では，相互再帰を強引に1つの再帰にまとめている．この場合，型情報が失われるため，元のデータ型では作れない再帰構造も作れてしまい， `cata` オペレータに動的型チェックが必要になるなど，使い勝手の悪いコードになる．しかしこの手法では，一応元の再帰構造を捉えてはいる．今回の記事で紹介する recursion schemes for higher algebra (高階代数を元にした再帰スキーム)とは，基本的には相互再帰を上のように表現する際，型情報が失われないようなんとかする手法である．

## Recursion Schemes for Higher Algebra

先ほどの相互再帰を `Fix` で固定する表現の問題点は，再帰構造を表す `Functor` に型情報が足りないことである．つまり， `ExprF :+: DeclF` という型では， `ExprF` か `DeclF` かのどちらかであるという情報しかなく，実際にどちらかの情報は実行時にしか分からない．もし，型情報にどちらかを確定する情報があれば，それを利用して次の再帰の型を指定するようにすることで，型安全な相互再帰構造を表す `Functor` が作れるはずである．これが， recursion schemes for higher algebra による相互再帰構造表現の基本的なアイデアである．

今までは，単純に `Functor (f :: Type -> Type)` に対する recursion schemes を考えてきたが， recursion schemes for higher algebra は `Functor (f :: Type -> Type)` を `HFunctor (f :: (k -> Type) -> (k -> Type)` に拡張して考える． `HFunctor` とは `Functor` を高階にしたもので，以下のように定義される[^notice-functor-transformer]．

[^notice-functor-transformer]: 本当は `HFunctor f` は functor transformer ( `Functor g => Functor (f g)` ) であるという制約を入れたほうがいいのだが， Haskell の型システム上あまり意味のある制約を入れることは難しい．ところで，少なくとも `hfmapLack $ Nat (fmap id) = id` は成り立つので，( `hfmap` と整合性のとれる) transformer が与えられた時必ずそれは `Functor` になることが， free theorem から示せる．なので，ここでは functor transformer であることは，特に要求しない．

```haskell
newtype f :~> g = Nat
  { unNat :: forall i. f i -> g i
  }
  
instance Category (:~>) where
  id = Nat Prelude.id
  Nat f . Nat g = Nat (f Prelude.. g)

-- | Higher order functor
--
-- > hfmap id = id
-- > hfmap f . hfmap g = hfmap (f . g)
--
class HFunctor (f :: (k -> Type) -> (k -> Type)) where
  hfmap :: (a :~> b) -> (f a :~> f b)

hfmapLack :: HFunctor f => a :~> b -> f a i -> f b i
hfmapLack f = unNat (hfmap f)
```

`HFunctor` のインスタンス例としては，例えば以下のものがある．

```haskell
import Data.Functor.Compose

instance Functor f => HFunctor (Compose f) where
  hfmap (Nat f) = Nat (Compose . fmap f . getCompose)
```

この `HFunctor` を使って相互再帰をうまく表現する方法を考える．例えば， `Int` 型の次は `Char` 型， `Char` 型の次は `Int` 型というような一見不思議なリストを，次のように定義する．

```haskell
data IList = ICons Int CList | INil
data CList = CCons Char IList | CNil

cataICList :: (Either Int Char -> b -> b) -> b -> (Either IList CList -> b)
cataICList f e = either goI goC
  where
    goI (ICons x xs) = f (Left x) (goC xs)
    goI INil         = e
    
    goC (CCons x xs) = f (Right x) (goI xs)
    goC CNil         = e
```

このデータ型は相互再帰となっている．これを，次のような `HFunctor` を考えることで，表現する．

```haskell
data ICListF r i where
  IConsF :: Int -> r Char -> ICListF r Int
  CConsF :: Char -> r Int -> ICListF r Char
  ICNilF :: ICListF r i

instance HFunctor ICListF where
  hfmap (Nat f) = Nat \case
    IConsF x m -> IConsF x (f m)
    CConsF x m -> CConsF x (f m)
    ICNilF     -> ICNilF

newtype HFix f i = HIn
  { hout :: f (HFix f) i
  }
  
hcata :: HFunctor f => (f b :~> b) -> (HFix f :~> b)
hcata f = go
  where
    go = f . hfmap go . Nat hout

type HFBICList = HFix ICListF

hcataHFBICList :: (ICListF b :~> b) -> (HFBICList :~> b)
hcataHFBICList = hcata
```

実際にどういうリストになるか確認してみる．

```haskell
>>> deriving instance (forall i. Show (r i)) => Show (ICListF r j)
>>> deriving instance (forall r j. (forall i. Show (r i)) => Show (f r j)) => Show (HFix f k)
>>> HIn . IConsF 0 $ HIn . CConsF 'c' $ HIn . IConsF 1 $ HIn ICNilF
HIn {hout = IConsF 0 (HIn {hout = CConsF 'c' (HIn {hout = IConsF 1 (HIn {hout = ICNilF})})})}
>>> HIn . IConsF 0 $ HIn . IConsF 1 $ HIn ICNilF

<interactive>:229:18: error:
    • Couldn't match type ‘Int’ with ‘Char’
      Expected type: HFix ICListF Char -> HFix ICListF Char
        Actual type: HFix ICListF Char -> HFix ICListF Int
    • The first argument of ($) takes one argument,
      its type is ‘cat0 a0 c0’,
      it is specialized to ‘HFix ICListF Char -> HFix ICListF Int’
      In the second argument of ‘($)’, namely
        ‘HIn . IConsF 1 $ HIn ICNilF’
      In the expression: HIn . IConsF 0 $ HIn . IConsF 1 $ HIn ICNilF
```

ちゃんと `Int` が2回続くようなものは作れないようになっていることが分かる．

この表現がやってることのイメージは， `ICListF r i` の `r` の部分が高階の再帰部分を受け取る場所で，次の `i` はその `Functor` の種類を表すものという感じである． `ICListF r Int` は `IList` の再帰部分， `ICListF r Char` は `CList` の再帰部分というように再帰部分の族を形成している．従来の `Fix` で固定する方法の問題は， `f :+: g` という型が `f` か `g` のどちらかを表しているのかが分からなかった． `ICListF r i` は `ICListF r Int + ICListF r Char` と表現の幅は変わらないが， `i` の型を指定することで `Int` なら `IList` の再帰部分だけ， `Char` なら `CList` の再帰部分だけというように射影を行うことができる．よって，再帰構造を表す `Functor` で `r Int` や `r Char` というように射影を指定してやることで続きの部分を制限することができる．

この表現を使って，最初の相互再帰を含むAST表現は，次のように表すことができる．

```haskell
type VarName = String

data AstTag
  = ExprTag
  | DeclTag

data AstF r i where
  AstExprF :: ExprF r -> AstF r 'ExprTag
  AstDeclF :: DeclF r -> AstF r 'DeclTag

data ExprF r
  = LamAbsF VarName (r 'ExprTag)
  | AppF (r 'ExprTag) (r 'ExprTag)
  | LetF [r 'DeclTag] (r 'ExprTag)
  | VarF VarName
  | NumLitF Int
  
data DeclF r
  = DeclF VarName (r 'ExprTag)


instance HFunctor AstF where
  hfmap (Nat f) = Nat \case
    AstExprF (LamAbsF x e) -> AstExprF $ LamAbsF x (f e)
    AstExprF (AppF e1 e2)  -> AstExprF $ AppF (f e1) (f e2)
    AstExprF (LetF ds e)   -> AstExprF $ LetF (f <$> ds) (f e)
    AstExprF (VarF x)      -> AstExprF $ VarF x
    AstExprF (NumLitF x)   -> AstExprF $ NumLitF x
    AstDeclF (DeclF x e)   -> AstDeclF $ DeclF x (f e)


type Ast = HFix AstF

hcataAst :: (AstF b :~> b) -> (Ast :~> b)
hcataAst = hcata
```

この表現により汎用的にオペレータを定義できる他， `Fix` において使用されている手法を `HFix` に拡張することで，相互再帰を扱いながら既存の手法を使用することができる．

## Higher Order Open Union

`HFix` を使う上では，相互再帰的なデータ型を一度和をとって1つにまとめる必要がある．この和を上では一々作成していたが， Data types a la carte のインターフェースを使うことで，和を作成する部分も共通化できる．ただ，Data types a la carteのインターフェースはいくつか問題点を抱えており，今日ではより扱いやすくなった，型レベルリストを元にした open union と member 関係という手法によって置き換えられている．ここでは，open unionを使用して，和を共通化する方法を紹介する．

open unionは複数の型の和を表現するデータ構造である．主な仕組みとしては，どの型の和であるかを型レベルリストで表し，データ表現として型情報の代わりに型レベルリストのインデックスを用い，コンストラクトとデコンストラクトの際型検査を行いながらインデックスを割り出しその情報を元にキャストを行う．

例えば， `HFunctor` の和は次のように実装できる．

```haskell
import Data.Extensible.Internal

data HSum f g r i = HInL (f r i) | HInR (g r i)

hsum :: f r :~> a -> g r :~> a -> HSum f g r :~> a
hsum (Nat lf) (Nat rf) = Nat \case
  HInL x -> lf x
  HInR x -> rf x

instance (HFunctor f, HFunctor g) => HFunctor (HSum f g) where
  hfmap f = hsum (Nat HInL . hfmap f) (Nat HInR . hfmap f)


data HUnion fs r i where
  HUnion :: Membership fs f -> f r i -> HUnion fs r i

injectU :: Member fs f => f r :~> HUnion fs r
injectU = Nat \x -> HUnion membership x

decomp :: HUnion (f ': fs) r :~> HSum (HUnion fs) f r
decomp = Nat \case
  HUnion n x -> leadership n (\Refl -> HInR x) (\m -> HInL $ HUnion m x)

comp :: HSum (HUnion fs) f r :~> HUnion (f ': fs) r
comp = hsum weaken $ Nat \x -> HUnion here x

weaken :: HUnion fs r :~> HUnion (f ': fs) r
weaken = Nat \case
  HUnion n x -> HUnion (navNext n) x

absurdU :: HUnion '[] r :~> a
absurdU = Nat \case
  HUnion n _ -> impossibleMembership n
 

instance HFunctor (HUnion '[]) where
  hfmap _ = absurdU
  
instance (HFunctor f, HFunctor (HUnion fs)) => HFunctor (HUnion (f ': fs)) where
  hfmap f = comp . hfmap f . decomp
```

型レベルリストの管理には， [extensibleパッケージ](https://hackage.haskell.org/package/extensible) を使っている．この `HUnion` を使うと，先ほどのASTデータは次のように書き直せる．

```haskell
type VarName = String

data AstTag
  = ExprTag
  | DeclTag

data ExprF r i where
  LamAbsF :: VarName -> r 'ExprTag -> ExprF r 'ExprTag
  AppF :: r 'ExprTag -> r 'ExprTag -> ExprF r 'ExprTag
  LetF :: [r 'DeclTag] -> r 'ExprTag -> ExprF r 'ExprTag
  VarF :: VarName -> ExprF r 'ExprTag
  NumLitF :: Int -> ExprF r 'ExprTag
  
instance HFunctor ExprF where
  hfmap (Nat f) = Nat \case
    LamAbsF x e -> LamAbsF x (f e)
    AppF e1 e2  -> AppF (f e1) (f e2)
    LetF ds e   -> LetF (f <$> ds) (f e)
    VarF x      -> VarF x
    NumLitF x   -> NumLitF x


data DeclF r i where
  DeclF :: VarName -> r 'ExprTag -> DeclF r 'DeclTag

instance HFunctor DeclF where
  hfmap (Nat f) = Nat \case
    DeclF x e -> DeclF x (f e)


type AstF = '[ExprF, DeclF]
type Ast = HFix (HUnion AstF)

hcataAst :: (HUnion AstF b :~> b) -> (Ast :~> b)
hcataAst = hcata
```

実際に，データを記述するときは，以下のように行う．

```haskell
injAst :: Member AstF f => f Ast i -> Ast i
injAst m = HIn $ unNat injectU m

-- \v. let f = (+ 1) in f v 
sampleAst :: Ast 'ExprTag
sampleAst = injAst . LamAbsF "v"
  . injAst . LetF
    [ injAst . DeclF "f" . injAst . AppF (injAst $ VarF "+") . injAst $ NumLitF 1
    ]
  . injAst . AppF (injAst $ VarF "f") . injAst $ VarF "v"
```

これによって， `HFunctor` の和を逐一定義する必要はなくなり，また再帰データ型を容易に拡張することが可能になる．

## Higher Order Cofree Annotation

次のようなデータ型を考えることで， cofree annotation も容易に拡張できる．

```haskell
data HCofreeF f a r i = HCofreeF (a i) (f r i)

instance HFunctor f => HFunctor (HCofreeF f a) where
  hfmap f = Nat \case
    HCofreeF x m -> HCofreeF x $ hfmapLack f m


type AstWithAnn a = HFix (HCofreeF (HUnion AstF) a)
```

後はアノテーション用に，次のようなデータを用意してやれば良い．

```haskell
import Data.Extensible hiding ((<:), nil)
import Data.Extensible.HList
import Data.Profunctor (dimap)
import Control.Lens (view)
import GHC.OverloadedLabels

newtype RevApply a f = RevApply
  { getRevApply :: f a
  }

instance Wrapper (RevApply i) where
  type Repr (RevApply i) f = f i
  _Wrapper = dimap getRevApply (fmap RevApply)

newtype Ann fs i = AnnRecord
  { getAnnRecord :: RecordOf (RevApply i) fs
  }

annBuild :: HList (Field (RevApply i)) fs -> Ann fs i
annBuild = AnnRecord . fromHList

(<:) :: h x -> HList h xs -> HList h (x ': xs)
(<:) = HCons

infixr 0 <:

nil :: HList h '[]
nil = HNil


newtype AnnFieldGetter fs f = AnnFieldGetter
  { getField :: Ann fs :~> f
  }
instance Associate k f fs => IsLabel k (AnnFieldGetter fs f) where
  fromLabel = AnnFieldGetter
    $ Nat (view (itemAssoc (Proxy :: Proxy k)) . getAnnRecord)


data SourcePos i = SourcePos
  { positionLine :: Int
  , positionColumn :: Int
  }

newtype FreeVarOccur i = FreeVarOccur [VarName]

type RenamedAstAnn = Ann
  '[ "sourcePos" ':> SourcePos
  ,  "freeVarOccur" ':> FreeVarOccur
  ]
type RenamedAst = AstWithAnn RenamedAstAnn


sampleAnn :: RenamedAstAnn i
sampleAnn = annBuild
  $  #sourcePos @= SourcePos 10 3
  <: #freeVarOccur @= FreeVarOccur ["v", "f"]
  <: nil
  
sampleSourcePos :: SourcePos i
sampleSourcePos = unNat (getField #sourcePos) sampleAnn
```

`HFix` による表現は，より柔軟なアノテーションを埋め込める．例えば， `DeclF` は型情報を持つが， `ExprF` は型情報を持たないといった場合に，次のようなデータ型を用意することで，型安全にアノテーション情報を保持することができる．

```haskell
data TypeInfo

data TypeAnn i where
  HasType :: TypeInfo -> TypeAnn 'ExprTag
  NoType :: TypeAnn 'DeclTag
```

## まとめ

高階代数の recursion schemes を考えることにより，相互再帰を型安全に扱える方法を紹介した．また， `HFunctor` の和型と cofree annotation の拡張についても紹介した．

この後は，この手法を元にしたASTを使ってパーサとインタプリタを書いていく続編を書く予定だ．もし興味があったら，またそちらも見てみて欲しい．では，この辺でノシ


